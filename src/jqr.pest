WHITESPACE = _{ " " }

arrayIndex = @{ ASCII_DIGIT+ }

indexAccess = { "[" ~ arrayIndex ~ ( "," ~ arrayIndex )* ~ "]" }

singleKeyword = @{ ASCII_ALPHANUMERIC+ }

multiKeyword = { singleKeyword ~ ( "." ~ singleKeyword )* }

stringContent = { (ASCII_ALPHANUMERIC | "-" | ":"  )* }

stringValue = _{ QUOTATION_MARK ~ stringContent  ~ QUOTATION_MARK }

assignProp = { singleKeyword ~ ":=" ~  jqExpr }

newObjExpr = { "{" ~ assignProp ~ ( ";" ~ assignProp)* ~ "}" }

queryExpr = _{ indexAccess | keysExpr  | valsExpr | countExpr | multiKeyword }

integerValue = @{ ASCII_DIGIT+ }

floatValue = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

filterValue = _{ stringValue | floatValue | integerValue }

filterOp = { "=" | ">"  | "<" }

filterExpr = { queryExpr ~ filterOp ~ filterValue }

valsExpr = { ".vals" }

keysExpr = { ".keys" }

countExpr = { ".count" }

multiExpr = { ( filterExpr | queryExpr | newObjExpr )  ~ ( "|" ~  (filterExpr | queryExpr | newObjExpr) )+ }

jqExpr = _{ multiExpr | filterExpr | newObjExpr | queryExpr  }
